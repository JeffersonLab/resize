plugins {
    id 'war'
}
description = "Image Resize Web Service"
group = 'org.jlab'
version = new File("${projectDir}/VERSION").text.trim()
ext.version = project.version
ext.releaseDate = new Date().format('MMM dd yyyy')
tasks.withType(JavaCompile).configureEach {
    options.release = 11
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-Xlint:deprecation", "-Xlint:unchecked"]
}
repositories {
    mavenCentral()
}
dependencies {
    providedCompile 'javax:javaee-api:8.0.1',
                    'javax.servlet:jstl:1.2'
}
war {
    archiveFileName = 'resize.war'
    filesMatching('WEB-INF/web.xml') {
        filter {
            String line -> line.replaceAll("@VERSION@", version)
        }
        filter {
            String line -> line.replaceAll("@RELEASE_DATE@", releaseDate)
        }
    }
}